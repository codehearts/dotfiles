" codehearts .vimrc
" github.com/codehearts/dotfiles
"

call plug#begin('~/.vim/plugged')

" vim
Plug 'tpope/vim-sensible'

" colors
Plug 'lu-ren/SerialExperimentsLain'
Plug 'rhysd/vim-color-spring-night'
Plug 'soft-aesthetic/soft-era-vim'
Plug 'vectorstorm/vim-chlordane'
Plug 'reedes/vim-colors-pencil'
Plug 'cseelus/vim-colors-lucid'
Plug 'srcery-colors/srcery-vim'
Plug 'dennougorilla/azuki.vim'
Plug 'cseelus/vim-colors-tone'
Plug 'tjammer/blayu.vim'
Plug 'trusktr/seti.vim'
Plug 'jyota/vimColors' " eva, scooby

" writing
Plug 'garbas/vim-snipmate' | Plug 'honza/vim-snippets' | Plug 'MarcWeber/vim-addon-mw-utils' | Plug 'tomtom/tlib_vim'
Plug 'terryma/vim-multiple-cursors'
Plug 'junegunn/vim-easy-align'
Plug 'vim-syntastic/syntastic'
Plug 'jiangmiao/auto-pairs'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-surround'
Plug 'ervandew/supertab'

" ui
Plug 'vim-airline/vim-airline-themes'
Plug 'vim-airline/vim-airline'
Plug 'scrooloose/nerdtree', { 'on':  'NERDTreeToggle' }
Plug 'mhinz/vim-startify'
Plug 'majutsushi/tagbar', { 'on':  'TagbarToggle' }

" git
Plug 'tpope/vim-fugitive'

" latex
Plug 'LaTeX-Box-Team/LaTeX-Box', { 'for': 'tex' }

call plug#end()

"
" interface
"

set bg=dark
colorscheme lucid

if has('gui_running') " gvim/mvim
  set go-=r go-=L go-=m go-=T " remove scrollbars and toolbar from gui

  if has('macunix') " macOS
    set gfn=Input\ Mono:h11

    " use `open` to view latex output on macOS
    let g:LatexBox_viewer='open'
  elseif has('win32') || has('win64') " windows
    set gfn=Hack:h8 go=c " don't use gui prompts
    set rop=type:directx,\gamma:1.5,contrast:0.5,geom:1,\renmode:5,taamode:1,level:0.5 " directx rendering
  else " linux
    set gfn=Meslo\ LG\ S\ for\ Powerline\ 8
  endif
endif

"
" settings
"

set et ts=2 sw=2 " use spaces, 1 tab is 2 spaces
set noswf " don't use swap files
set ic scs hls " ignore case, smart case, highlight search matches
set enc=utf-8 " use utf-8 encoding
set nu rnu " show relative line numbers

"
" plugin assignments
"

" use symbols with airline
let g:airline_powerline_fonts=1
let g:airline_theme='lucius'
" compile latex asynchronously on write, quickfix won't steal cursor
let g:LatexBox_latexmk_preview_continuously=1
let g:LatexBox_latexmk_async=1
let g:LatexBox_quickfix=2
" use nicer symbols with syntastic
let g:syntastic_error_symbol='✖'
let g:syntastic_warning_symbol='⚠'
" set <leader> to ,
let mapleader=","

"
" bindings
"

" j/k stay on wrapped lines
map j gj
map k gk
" switch windows with ctrl-h/j/k/l
map <C-h> <C-W>h
map <C-j> <C-W>j
map <C-k> <C-W>k
map <C-l> <C-W>l
" write using sudo
cmap w!! %!sudo tee > /dev/null %

" write
nmap <leader>w :w!<cr>
" tabs
map <leader>tn :tabnew<cr>
map <leader>tc :tabclose<cr>
map <leader>th :tabm-1<cr>
map <leader>tl :tabm+1<cr>
" cd to open buffer's pwd
map <leader>cd :cd %:p:h<cr>:pwd<cr>
" spell checking
map <leader>ss :setlocal spell!<cr>
" vimgrep (manual typing, word under cursor)
map <leader>gr :vimgrep //gj ./**/* <bar> cw<c-left><c-left><c-left><c-left><right>
xmap <leader>gr y:vimgrep '<c-r>"' ./**/* <bar> cw<cr>
" regex replace (manual typing, word under cursor)
map <leader>re :%s //g<left><left>
xmap <leader>re y:%s /<c-r>"//g<c-right><left><left>
" nerd tree
map <leader>nt :NERDTreeToggle<cr>
map <leader>mt :NERDTreeMirror<cr>
" tagbar
map <leader>tb :TagbarToggle<cr>
" easy align
xmap ga <plug>(EasyAlign)
nmap ga <plug>(EasyAlign)

"
" file types
"

autocmd FileType mail setlocal spell spl=en_us " spellcheck email
autocmd FileType mail setlocal fo+=aw          " don't automatically break lines in emails

"
" startify header
"

function! s:center_text(lines) abort
  let longest_line   = strwidth(a:lines[0])
  let centered_lines = map(copy(a:lines),
    \ 'repeat(" ", (&columns / 2) - (longest_line / 2)) . v:val')
  return centered_lines
endfunction

let g:wired_ascii = [
  \ '                       `--.                       ',
  \ '                   ``.-ssss-.``                   ',
  \ '               `..-----/oo+------..               ',
  \ '            `.---::::::://:::::::---.`            ',
  \ '          `-::::::/++oooooooo++/::::::-`          ',
  \ '        ./ssss//+osssssssssssssso+/:osss+-`       ',
  \ '     `-+ssso+/+osssso++++++++osssso+/+ossso/.     ',
  \ '   ./osso/::/+ssss+++ooossooo+++sssso/::/+sss+.   ',
  \ ' `+sss+:-:::+ssso++osssssssssso++osss+:::-:/sss+. ',
  \ '.oss+..--::/ssss++osssssssssssso++osss/::--..+sss-',
  \ 'osss` .-:::/ssso++sssssssssssssoo+osss/:::--  osso',
  \ '+sss. `-:::/ssso++sssssssssssssoo+osss/:::-- `ssso',
  \ '`+sss:.--::/osss++osssssssssssso++ssss/::--.:osso.',
  \ ' `:osso/::::/osss++oossssssssoo++ssss+::::/osso:` ',
  \ '   `:+ssso+/:/ossso+++oooooo+++ossso+://oosso:`   ',
  \ '  -::`.:ossso+/+ossssoo////oossssso//ossso/..-:-  ',
  \ ' -sss+  `./oso/:/+ossss/:::sssso+/::oso+.`  +sss- ',
  \ ' `:+/.     `---::::osss----osso:::::--.     ./+:` ',
  \ '             `.---:osso....+sso:----`             ',
  \ '                `.-+sso````+sso..``               ',
  \ '          -/:`     /sso    +ss+      :/-          ',
  \ '         .sss:     +sso    +sso     -sss.         ',
  \ '         `ssso.  `:sss+    /sss/.  .+sss.         ',
  \ '          -ssssssssss+`     +ssssssssss:          ',
  \ '           `-/+oo+/:.        .:/+oo+/-`           ',
  \ ]
let g:startify_custom_header = s:center_text(g:wired_ascii)
